apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

buildscript {
    ext.kotlin_version = '1.4.20'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'libs'
    }
}
def localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())
def keystoreProperties = new Properties()
keystoreProperties.load(rootProject.file("keystore.properties").newDataInputStream())

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId localProperties["id"]
        versionCode 31
        versionName "1.30"
        multiDexEnabled true
        minSdkVersion 21
        targetSdkVersion 30
        buildConfigField "String", "PIXA_KEY", localProperties["pixa_key"]
        buildConfigField "String", "SOUND_CLOUD_KEY", localProperties["s_c_key"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res', 'apollo/res']
            aidl.srcDirs = ['src', 'apollo/src']
            assets.srcDirs = ['assets']
            java {
                srcDir '../common/src/main/java'
                srcDir 'apollo/src'
                srcDir 'src'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "2g"
        jumboMode = true
    }

    signingConfigs {
        zig {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.zig
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "MediaTor.apk"
                }
            }
        }
    }
    buildToolsVersion '29.0.3'
}

dependencies {
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.google.android:flexbox:1.0.0'
    implementation 'com.facebook.android:audience-network-sdk:6.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.re2j:re2j:1.5'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation files('libs/jlibtorrent-1.2.10.0.jar')
    implementation files('libs/jlibtorrent-android-arm-1.2.10.0.jar')
    implementation files('libs/jlibtorrent-android-x86-1.2.10.0.jar')
    implementation files('libs/jlibtorrent-android-arm64-1.2.10.0.jar')
    implementation files('libs/jlibtorrent-android-x86_64-1.2.10.0.jar')
}
